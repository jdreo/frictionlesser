import datetime

configfile: "config.yaml"

# NOW=datetime.date.today().isoformat()
# workdir: "expe_{name}_{date}".format(name=config["expe-name"], date=NOW)

SEEDS=list(range(0,config["runs"]))

FRICTIONLESSER=config["executable"]

rule all:
    input:
        expand("signatures_of_{size}-genes/signature_{seed}.txt", size=config["sizes"], seed=SEEDS)


# rule rank:
#     input:
#         "exprs.csv"
#     output:
#         "ranks.tsv"
#     shell:
#         "{FRICTIONLESSER}"
#         "  --exprs={input}"
#         "  > {output}"

rule save_cache_transcriptome:
    input:
        "ranks.tsv"
    output:
        protected("cache/trans.cache.dat")
    log: "logs/save_cache_transcriptome.log"
    benchmark: "logs/save_cache_transcriptome.bench"
    resources:
        job_name="cache_trans"
    shell:
        "{FRICTIONLESSER}"
        "  --ranks={input}"
        "  --cache-transcriptome={output}"
        "  --cache-only"
        "  2> {log}"

rule save_cache_size:
    input:
        ranks="ranks.tsv",
        transcache="cache/trans.cache.dat"
    output:
        protected("cache/size_{size}.cache.dat")
    wildcard_constraints:
        # Wildcard {size} should be numeric.
        size="\d+"
    log: "logs/save_cache_size-{size}.log"
    benchmark: "logs/save_cache_size-{size}.bench"
    resources:
        job_name=lambda wildcards: f"cache_z{wildcards.size}"
    shell:
        "{FRICTIONLESSER}"
        "  --ranks={input.ranks}"
        "  --cache-transcriptome={input.transcache}"
        "  --cache-size={output}"
        "  --cache-only"
        "  2> {log}"

rule load_and_run:
    input:
        ranks="ranks.tsv",
        transcache="cache/trans.cache.dat",
        sizecache="cache/size_{size}.cache.dat"
    output:
        "signatures_of_{size}-genes/signature_{seed}.txt"
    wildcard_constraints:
        # Wildcards should be numeric.
        seed="\d+",
        size="\d+"
    log: "logs/load_and_run_size-{size}_seed-{seed}.log"
    benchmark: "logs/load_and_run_size-{size}_seed-{seed}.bench"
    resources:
        job_name=lambda wildcards: f"run_z{wildcards.size}_s{wildcards.seed}"
    shell:
        "{FRICTIONLESSER}"
        "  --ranks={input.ranks}"
        "  --cache-transcriptome={input.transcache}"
        "  --cache-size={input.sizecache}"
        "  --ngenes={wildcards.size}"
        "  --seed={wildcards.seed}"
        "  > {output}"
        "  2> {log}"

