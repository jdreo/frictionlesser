import datetime

EXPE="test"
SIZES=[5,10]
SEEDS=[1,2]

# From workdir: one more dir up.
FRICTIONLESSER="../../../release/app/frictionlesser"

NOW=datetime.datetime.now().replace(microsecond=0).isoformat()

workdir: "expe_{name}_{date}".format(name=EXPE, date=NOW)

rule all:
    input:
        expand("size_{size}-genes/signature_{seed}.txt", size=SIZES, seed=SEEDS)

rule download:
    output:
        "GSE131928_RAW.tar"
    shell:
        "wget ftp://ftp.ncbi.nlm.nih.gov/geo/series/GSE131nnn/GSE131928/suppl/GSE131928_RAW.tar"

rule extract:
    input:
        "GSE131928_RAW.tar"
    output:
        "GSM3828672_Smartseq2_GBM_IDHwt_processed_TPM.tsv.gz",
        "GSM3828673_10X_GBM_IDHwt_processed_TPM.tsv.gz"
    shell:
        "tar xf {input}"

rule uncompress:
    input:
        "{file}.tsv.gz"
    output:
        "{file}.tsv"
    shell:
        "gunzip {input}"

rule shorten_data:
    input:
        "GSM3828672_Smartseq2_GBM_IDHwt_processed_TPM.tsv"
    output:
        "exprs.csv"
    shell:
        # set +o pipefail: disable failing on pipe error, to bypass a qsv bug.
        # qsv transpose: transpose AND CONVERT TO CSV
        # head: shorten columns
        # qsv transpose: untranspose
        # head: shorten lines
        "set +o pipefail ; qsv transpose {input} | head -n 20 | qsv transpose | head -n 20 > {output}"

rule rank:
    input:
        "exprs.csv"
    output:
        "ranks.tsv"
    shell:
        "{FRICTIONLESSER}"
        "  --exprs={input}"
        "  > {output}"

rule save_cache_transcriptome:
    input:
        "ranks.tsv"
    output:
        protected("cache/trans.cache.dat")
    shell:
        "{FRICTIONLESSER}"
        "  --ranks={input}"
        "  --cache-transcriptome={output}"
        "  --cache-only"

rule save_cache_size:
    input:
        ranks="ranks.tsv",
        transcache="cache/trans.cache.dat"
    output:
        protected("cache/size_{size}.cache.dat")
    wildcard_constraints:
        # Wildcard {size} should be numeric.
        size="\d+"
    shell:
        "{FRICTIONLESSER}"
        "  --ranks={input.ranks}"
        "  --cache-transcriptome={input.transcache}"
        "  --cache-size={output}"
        "  --cache-only"

rule load_cache_run:
    input:
        ranks="ranks.tsv",
        transcache="cache/trans.cache.dat",
        sizecache="cache/size_{size}.cache.dat"
    output:
        "size_{size}-genes/signature_{seed}.txt"
    wildcard_constraints:
        # Wildcards should be numeric.
        seed="\d+",
        size="\d+"
    shell:
        "{FRICTIONLESSER}"
        "  --ranks={input.ranks}"
        "  --cache-transcriptome={input.transcache}"
        "  --cache-size={input.sizecache}"
        "  --ngenes={wildcards.size}"
        "  --seed={wildcards.seed}"
        "  > {output}"
